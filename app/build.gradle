apply plugin: 'com.android.application'
apply plugin: 'org.sonarqube'

repositories {
    maven { url 'https://jitpack.io' }
    maven { url 'https://plugins.gradle.org/m2' }
}

sonarqube {
    properties {
        property 'sonar.projectName', 'WakeBlock'
        property 'sonar.projectKey', 'com.giovannibozzano.wakeblock'
        property 'sonar.host.url', 'http://localhost:9000'
        property 'sonar.language', 'java'
        property 'sonar.sources', 'src/main/'
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'
    defaultConfig {
        applicationId 'com.giovannibozzano.wakeblock'
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 47
        versionName '0.5.3'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.google.android.material:material:1.3.0-alpha02'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.4'
    implementation 'com.jakewharton:butterknife:10.2.0'
    implementation 'com.github.topjohnwu:libsu:1.1.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.smali:smali:2.4.0'
    implementation 'org.smali:baksmali:2.4.0'
    testImplementation 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.0'
}

def properties = new Properties()
def propertiesFile = file('keystore.properties')
if (propertiesFile.canRead()) {
    properties.load(new FileInputStream(propertiesFile))
    if (properties != null && properties.containsKey('storeFile') && properties.containsKey('storePassword') && properties.containsKey('keyAlias') && properties.containsKey('keyPassword')) {
        android.signingConfigs.release.storeFile = file(properties['storeFile'])
        android.signingConfigs.release.storePassword = properties['storePassword']
        android.signingConfigs.release.keyAlias = properties['keyAlias']
        android.signingConfigs.release.keyPassword = properties['keyPassword']
    }
}
